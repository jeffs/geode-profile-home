# VARIABLES

CASE_SENSITIVE=true
COMPLETION_WAITING_DOTS=true
DISABLE_AUTO_TITLE=true
DISABLE_UNTRACKED_FILES_DIRTY=true

# Note that zsh-syntax-highlighting must be the last plugin sourced.
# https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/INSTALL.md
plugins=(zsh-syntax-highlighting)

source $ZSH/oh-my-zsh.sh

# Use precmd to format a nice separator for use in a colorized PROMPT.
typeset -H prompt_bar
PROMPT=$'\n%{$fg[cyan]%}(%h) $(hostname):$prompt_bar\n$%{$reset_color%} '
precmd() {
    local d=$(dirs -p | head -1)
    local -i n=$(($COLUMNS - ${#d} - ${#$(hostname)} - 10))
    prompt_bar="$d $(printf 'â”€%.0s' $(=/usr/bin/seq $n))"
}

# OPTIONS

setopt rmstarsilent	# Don't confirm rm *.

unsetopt autocd
unsetopt auto_pushd
unsetopt correct_all
unsetopt hist_verify
unsetopt share_history

zstyle ':completion:*' matcher-list ''

# ALIASES

unalias l la

alias cal='ncal -b'
alias tree='exa --group-directories-first --tree'
alias t='tree --git-ignore'
alias h=history
alias u='c ..'
alias uu='c ../..'
alias uuu='c ../../..'
alias py3='python3 -q'
alias play='c ~/var/rust/play && nvim src/bin/play.rs'
for i in `seq 9`; do alias t$i="t -L$i"; done
for s in br ci co di flog glog pull push st stash; do alias $s="git $s"; done

# FUNCTIONS

# Useful when copying lines of code from documentation that includes prompts.
function $ { "$@" }

c() { cd "$@" && ls; }
cg() { c $(git rev-parse --show-toplevel); }
cl() { mc ~/file/log/$(date +%Y/%m/%d); }
cy() { mc ~/file/log/$(yesterday); }
e() { ${EDITOR:-nvim} "$@"; }
l() { ls -hl --color "$@" | $PAGER }
la() { ls -Ahl --color "$@" | $PAGER }
mc() { mkdir -p $1 && c $1; }

# Load nvm lazily, since merely sourcing its definition takes ~350ms.
nvm() {
    # Install NVM if necessary.
    if [ ! -d ~/.nvm ]; then
        ~/conf/bin/install-node.zsh || return 1
    fi

    unset -f nvm
    export NVM_DIR=~/.nvm
    source $NVM_DIR/nvm.sh
    source $NVM_DIR/bash_completion
    nvm "$@"
    export PATH="$HOME/.nvm/versions/node/$(nvm current)/bin:$PATH"
}

for command in node npm npx yarn; do
    $command() {
        unset -f node npm npx yarn
        [[ -v NVM_DIR ]] || nvm use --lts
        "$0" "$@"
    }
done
unset command

# Load Scala support lazily, to avoid slowing shell startup.
sbt() {
    if ! whence -p sbt >&/dev/null; then
        export SDKMAN_DIR=~/.sdkman
        # Install sdkman, java, and sbt if necessary.
        if [ ! -d "$SDKMAN_DIR" ]; then
            ~/conf/bin/install-scala.zsh || return 1
        fi
        source "$SDKMAN_DIR"/bin/sdkman-init.sh
    fi
    unset -f sbt
    sbt "$@"
}

exa() {
    if ! /bin/which exa >&/dev/null; then
        cargo install exa
    fi
    unset -f exa
    exa "$@"
}

gh() {
    if [ ! -x ~/usr/bin/gh ]; then
        ~/conf/bin/install-gh.zsh || return 1
    fi
    unset -f gh
    gh "$@"
}

# Open a man page in Vim.
Man() { vim +":Man $1 | only"; }

# BINDINGS

if [ -d ~/opt/fzf ]; then
    source ~/opt/fzf/shell/completion.zsh 2>/dev/null
    source ~/opt/fzf/shell/key-bindings.zsh
fi

# https://github.com/ohmyzsh/ohmyzsh/issues/5071
bindkey '^[l' down-case-word

# Stolen by FZF.  Steal it back.
bindkey '^[c' capitalize-word
